package engine

import "core:mem"

CompData :: struct {
    owner: Transform_Handle `json:"-"`,
    local_id: Local_ID `inspect:"-"`,
    enabled: bool,
    nested_owned: bool `json:"-" inspect:"-"`,
}

comp_zero :: proc(p: ^$T) where
    offset_of(T, base) == 0,
    type_of(T{}.base) == CompData
{
    mem.zero(rawptr(uintptr(p) + size_of(CompData)), size_of(T) - size_of(CompData))
}

comp_init_base :: proc(comp: rawptr, owner: Transform_Handle) {
    w := ctx_world()
    t := pool_get(&w.transforms, Handle(owner))
    base := cast(^CompData)comp
    base.owner = owner
    base.enabled = true
    if t != nil && t.scene != nil {
        base.local_id = scene_next_id(t.scene)
    }
}

transform_add_comp :: proc(tH: Transform_Handle, key: TypeKey) -> (Owned, rawptr) {
    w := ctx_world()
    t := pool_get(&w.transforms, Handle(tH))
    if t == nil do return {}, nil

    handle, pComp := world_pool_create(w, key)
    if pComp == nil do return {}, nil

    comp_init_base(pComp, tH)
    type_reset(key, pComp)

    base := cast(^CompData)pComp
    owned := Owned{handle = handle, local_id = base.local_id}
    append(&t.components, owned)
    return owned, pComp
}

transform_get_or_add_comp :: proc(tH: Transform_Handle, $T: typeid) -> (Owned, ^T) {
    owned, pComp := transform_get_comp(tH, T)
    if pComp != nil do return owned, pComp
    key, ok := get_type_key_by_typeid(T)
    if !ok do return {}, nil
    new_owned, raw := transform_add_comp(tH, key)
    return new_owned, cast(^T)raw
}

transform_remove_comp :: proc(tH: Transform_Handle, comp_handle: Handle) {
    w := ctx_world()
    t := pool_get(&w.transforms, Handle(tH))
    if t == nil do return
    for i in 0 ..< len(t.components) {
        c := t.components[i]
        if c.handle.index == comp_handle.index && c.handle.generation == comp_handle.generation && c.handle.type_key == comp_handle.type_key {
            world_pool_destroy(w, comp_handle)
            ordered_remove(&t.components, i)
            return
        }
    }
}

@(component)
@(typ_guid={guid = "adaf3551-4704-4255-ad91-fde59441dc53"})
Script :: struct {
    using base: CompData `inspect:"-"`,
}

type_reset_procs: [TypeKey]proc(rawptr)

type_reset :: proc(key: TypeKey, ptr: rawptr) {
	if fn := type_reset_procs[key]; fn != nil do fn(ptr)
}

type_cleanup_procs: [TypeKey]proc(rawptr)

type_cleanup :: proc(key: TypeKey, ptr: rawptr) {
	if fn := type_cleanup_procs[key]; fn != nil do fn(ptr)
}

@(cleanup={type=string, priority=0})
type_cleanup_string_field :: proc(s: ^string) {
	delete(s^)
}

type_cleanup_by_typeid :: proc(tid: typeid, ptr: rawptr) {
	if key, ok := get_type_key_by_typeid(tid); ok {
		type_cleanup(key, ptr)
	}
}

component_on_validate_procs: [TypeKey]proc(rawptr)

component_on_validate :: proc(key: TypeKey, ptr: rawptr) {
	if fn := component_on_validate_procs[key]; fn != nil do fn(ptr)
}

component_on_destroy_procs: [TypeKey]proc(rawptr)

component_on_destroy :: proc(key: TypeKey, ptr: rawptr) {
	if fn := component_on_destroy_procs[key]; fn != nil do fn(ptr)
}
