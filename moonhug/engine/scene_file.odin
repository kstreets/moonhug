package engine

import "core:encoding/json"
import "core:os"
import "core:fmt"
import "core:strings"
import "base:runtime"

_remap_refs_in_value :: proc(ptr: rawptr, ti: ^runtime.Type_Info, remap: ^map[Local_ID]Local_ID) {
	if ptr == nil || ti == nil do return
	base := runtime.type_info_base(ti)
	if base == nil do return

	#partial switch info in base.variant {
	case runtime.Type_Info_Struct:
		tid := ti.id
		if tid == typeid_of(PPtr) {
			pptr := cast(^PPtr)ptr
			if pptr.local_id != 0 {
				if new_id, ok := remap[pptr.local_id]; ok {
					pptr.local_id = new_id
				}
			}
			return
		}
		if tid == typeid_of(Ref) {
			ref := cast(^Ref)ptr
			if ref.pptr.local_id != 0 {
				if new_id, ok := remap[ref.pptr.local_id]; ok {
					ref.pptr.local_id = new_id
				}
			}
			return
		}
		if tid == typeid_of(Ref_Local) || tid == typeid_of(Owned) {
			rl := cast(^Ref_Local)ptr
			if rl.local_id != 0 {
				if new_id, ok := remap[rl.local_id]; ok {
					rl.local_id = new_id
				}
			}
			return
		}

		count := int(info.field_count)
		for i in 0..<count {
			field_ptr := rawptr(uintptr(ptr) + info.offsets[i])
			_remap_refs_in_value(field_ptr, info.types[i], remap)
		}

	case runtime.Type_Info_Union:
		tag_ptr := rawptr(uintptr(ptr) + info.tag_offset)
		tag: i64
		switch info.tag_type.size {
		case 1: tag = i64((cast(^u8)tag_ptr)^)
		case 2: tag = i64((cast(^u16)tag_ptr)^)
		case 4: tag = i64((cast(^u32)tag_ptr)^)
		case 8: tag = i64((cast(^u64)tag_ptr)^)
		}
		idx := tag if info.no_nil else tag - 1
		if idx < 0 || int(idx) >= len(info.variants) do return
		variant_ti := info.variants[idx]
		_remap_refs_in_value(ptr, variant_ti, remap)

	case runtime.Type_Info_Dynamic_Array:
		dyn := cast(^runtime.Raw_Dynamic_Array)ptr
		if dyn.data == nil || dyn.len == 0 do return
		elem_size := info.elem_size
		for i in 0..<dyn.len {
			elem_ptr := rawptr(uintptr(dyn.data) + uintptr(i * elem_size))
			_remap_refs_in_value(elem_ptr, info.elem, remap)
		}

	case runtime.Type_Info_Array:
		elem_size := info.elem_size
		for i in 0..<info.count {
			elem_ptr := rawptr(uintptr(ptr) + uintptr(i * elem_size))
			_remap_refs_in_value(elem_ptr, info.elem, remap)
		}
	}
}

_collect_transform_tree :: proc(w: ^World, tH: Transform_Handle, sf: ^SceneFile) {
	t := pool_get(&w.transforms, Handle(tH))
	if t == nil do return
	if t.nested_owned do return

	t_copy := t^
	t_copy.name = strings.clone(t.name)
	t_copy.children = make([dynamic]Ref, 0, len(t.children))
	for child in t.children {
		ct := pool_get(&w.transforms, child.handle)
		if ct != nil && ct.nested_owned do continue
		append(&t_copy.children, child)
	}
	t_copy.components = make([dynamic]Owned, 0, len(t.components))
	for c in t.components {
		if c.handle.type_key == INVALID_TYPE_KEY do continue
		raw := world_pool_get(w, c.handle)
		if raw != nil {
			base := cast(^CompData)raw
			if base.nested_owned do continue
		}
		append(&t_copy.components, c)
	}
	append(&sf.transforms, t_copy)

	for &c in t.components {
		if c.handle.type_key == INVALID_TYPE_KEY do continue
		raw := world_pool_get(w, c.handle)
		if raw != nil {
			base := cast(^CompData)raw
			if base.nested_owned do continue
		}
		world_pool_collect(w, c.handle, sf)
	}

	for child in t.children {
		ct := pool_get(&w.transforms, child.handle)
		if ct != nil && ct.nested_owned do continue
		_collect_transform_tree(w, Transform_Handle(child.handle), sf)
	}
}

// Walks the nested-owned subtree for override capture. `outer_ns` is the
// NestedScene we're capturing for; `outer_host` is the live transform it
// resolves to. Items belonging to a *different* NS (inner prefabs nested under
// this one) live in their own namespace and would collide with the outer
// prefab's local_ids during diff (see Unity's PrefabInstance/m_Modifications
// model — overrides only address items in the immediate prefab). When we hit
// such a boundary we still serialize the host transform itself (it is the
// outer prefab's content), but stop pulling in its components and children.
_collect_nested_owned_subtree :: proc(
	w: ^World,
	tH: Transform_Handle,
	sf: ^SceneFile,
	root_local_id_override: Local_ID = 0,
	outer_ns: ^NestedScene = nil,
	outer_host: Transform_Handle = {},
) {
	t := pool_get(&w.transforms, Handle(tH))
	if t == nil do return

	is_inner_boundary := false
	if outer_ns != nil && tH != outer_host {
		owning := scene_find_nested_scene_for_host(t.scene, tH)
		if owning != nil && owning != outer_ns do is_inner_boundary = true
	}

	t_copy := t^
	if root_local_id_override != 0 do t_copy.local_id = root_local_id_override
	t_copy.name = strings.clone(t.name)
	t_copy.children = make([dynamic]Ref, 0, len(t.children))
	if !is_inner_boundary {
		for child in t.children {
			ct := pool_get(&w.transforms, child.handle)
			if ct != nil do append(&t_copy.children, child)
		}
	}
	t_copy.components = make([dynamic]Owned, 0, len(t.components))
	if !is_inner_boundary {
		for c in t.components {
			if c.handle.type_key == INVALID_TYPE_KEY do continue
			raw := world_pool_get(w, c.handle)
			if raw != nil {
				base := cast(^CompData)raw
				if base.nested_owned do append(&t_copy.components, c)
			}
		}
	}
	append(&sf.transforms, t_copy)

	if is_inner_boundary do return

	for &c in t.components {
		if c.handle.type_key == INVALID_TYPE_KEY do continue
		raw := world_pool_get(w, c.handle)
		if raw == nil do continue
		base := cast(^CompData)raw
		if base.nested_owned do world_pool_collect(w, c.handle, sf)
	}

	for child in t.children {
		ct := pool_get(&w.transforms, child.handle)
		if ct != nil && ct.nested_owned {
			_collect_nested_owned_subtree(w, Transform_Handle(child.handle), sf, 0, outer_ns, outer_host)
		}
	}
}

// Computes the prefab-chain baked base for `ns`: starting from `ns.source_prefab`'s
// raw, applies each prefab in the chain's NS-for-this-child overrides in order
// (outermost-first), producing the bytes that represent "what `ns` looks like
// before any root-scene overrides are applied." Caller owns the returned bytes
// when ok is true.
//
// For native NS (expand_parent == {}) this is just the prefab raw — no chain.
// For depth-N inner NS, walks N levels of outer prefab files.
@(private = "file")
_chain_baked_base_for_ns :: proc(s: ^Scene, ns: ^NestedScene) -> ([]byte, bool) {
	if s == nil || ns == nil do return nil, false

	prefab_raw, ok := scene_lib[ns.source_prefab]
	if !ok {
		if !scene_lib_register(ns.source_prefab) do return nil, false
		prefab_raw, ok = scene_lib[ns.source_prefab]
		if !ok do return nil, false
	}

	clone_raw :: proc(src: []byte) -> []byte {
		out := make([]byte, len(src))
		copy(out, src)
		return out
	}

	if ns.expand_parent == {} {
		return clone_raw(prefab_raw), true
	}

	// Build the chain of (outer_prefab_guid, transform_parent_in_outer) hops
	// from `ns` outward to root, then walk in reverse (outermost-first) and at
	// each level apply that level's outer prefab's NS-for-(next inner)
	// overrides to the next prefab raw. The final result is `ns.source_prefab`
	// raw with every level's overrides on top.
	Hop :: struct { outer_guid: Asset_GUID, child_guid: Asset_GUID, child_transform_parent: Local_ID }
	hops := make([dynamic]Hop, 0, 4, context.temp_allocator)

	cur := ns
	for _ in 0 ..< 64 {
		ep := cur.expand_parent
		if ep == {} do break
		outer := scene_find_nested_scene_for_host(s, ep)
		if outer == nil do return nil, false
		append(&hops, Hop{
			outer_guid             = outer.source_prefab,
			child_guid             = cur.source_prefab,
			child_transform_parent = cur.transform_parent,
		})
		cur = outer
	}

	// Walk hops in reverse (outermost-first). Start with outermost prefab raw,
	// each iteration extracts the NS-for-child overrides and applies them to
	// the child prefab raw.
	cur_raw := prefab_raw  // last iteration produces ns.source_prefab raw + chain mods
	cur_owns := false

	for i := len(hops) - 1; i >= 0; i -= 1 {
		hop := hops[i]
		outer_raw, ohas := scene_lib[hop.outer_guid]
		if !ohas {
			if !scene_lib_register(hop.outer_guid) do return nil, false
			outer_raw, ohas = scene_lib[hop.outer_guid]
			if !ohas do return nil, false
		}

		outer_copy := make([]byte, len(outer_raw), context.temp_allocator)
		copy(outer_copy, outer_raw)
		outer_sf: SceneFile
		if json.unmarshal(outer_copy, &outer_sf) != nil do return nil, false

		matching: []Override
		for &m in outer_sf.nested_scenes {
			if m.source_prefab != hop.child_guid do continue
			if m.transform_parent != hop.child_transform_parent do continue
			matching = m.overrides[:]
			break
		}

		child_raw, chas := scene_lib[hop.child_guid]
		if !chas {
			if !scene_lib_register(hop.child_guid) {
				scene_file_destroy(&outer_sf)
				return nil, false
			}
			child_raw, chas = scene_lib[hop.child_guid]
			if !chas {
				scene_file_destroy(&outer_sf)
				return nil, false
			}
		}

		baked := nested_scene_apply_overrides(child_raw, matching)
		baked_owns := raw_data(baked) != raw_data(child_raw)
		next_buf: []byte
		if baked_owns {
			next_buf = baked
		} else {
			// no overrides at this level — copy so we own uniformly.
			next_buf = clone_raw(child_raw)
		}
		scene_file_destroy(&outer_sf)

		if cur_owns do delete(cur_raw)
		cur_raw = next_buf
		cur_owns = true
	}

	if !cur_owns do return clone_raw(cur_raw), true
	return cur_raw, true
}

// Captures root-scene overrides for `ns` directly into the open scene's root
// native NS. Diffs `ns`'s prefab-chain-baked base against the live
// nested-owned subtree; each resulting (target, property_path, value) is
// emitted onto the **native** NS that owns this chain (with target rewritten
// to a breadcrumb local_id when `ns` is an inner NS, or kept as the prefab
// lid when `ns` IS native). Inner-NS records never accumulate overrides under
// this design — they are runtime artifacts only.
//
// Caller is responsible for clearing native_ns.overrides BEFORE the first
// call across all NS records, and for clearing every NS's overrides AFTER
// (the inner ones must end up empty so resolve and serialization see a
// consistent picture).
@(private = "file")
_capture_overrides_to_native :: proc(s: ^Scene, ns: ^NestedScene) {
	w := ctx_world()
	if ns.source_prefab == (Asset_GUID{}) do return

	host_tH := nested_scene_resolve_host_handle(s, ns)
	if host_tH == {} do return
	host_t := pool_get(&w.transforms, Handle(host_tH))
	if host_t == nil do return

	prefab_raw, has_prefab := scene_lib[ns.source_prefab]
	if !has_prefab {
		if !scene_lib_register(ns.source_prefab) do return
		prefab_raw, has_prefab = scene_lib[ns.source_prefab]
		if !has_prefab do return
	}

	prefab_root_id: Local_ID
	{
		prefab_copy := make([]byte, len(prefab_raw), context.temp_allocator)
		copy(prefab_copy, prefab_raw)
		base_sf: SceneFile
		if json.unmarshal(prefab_copy, &base_sf) == nil {
			prefab_root_id = base_sf.root
			scene_file_destroy(&base_sf)
		}
	}

	work_sf := SceneFile{}
	work_sf.root = prefab_root_id != 0 ? prefab_root_id : host_t.local_id
	_collect_nested_owned_subtree(w, host_tH, &work_sf, prefab_root_id, ns, host_tH)
	defer scene_file_destroy_shallow(&work_sf)

	work_raw, werr := json.marshal(work_sf, json.Marshal_Options{spec = .JSON, pretty = false})
	if werr != nil {
		fmt.printf("[Scene] Failed to marshal working copy for override capture: %v\n", werr)
		return
	}
	defer delete(work_raw)

	base_raw, ok := _chain_baked_base_for_ns(s, ns)
	if !ok do return
	defer delete(base_raw)

	diff := nested_scene_diff_overrides(base_raw, work_raw)
	defer {
		// `diff` ownership is transferred into native_ns.overrides (or freed if
		// any entries are skipped); destroy the dynamic-array shell at end.
		delete(diff)
	}

	_drop_overrides_with_missing_targets(&diff, prefab_raw)

	// Locate the native NS that owns this chain, plus the chain itself for
	// breadcrumb keying.
	native_ns: ^NestedScene = ns
	chain: [dynamic]PPtr = nil
	if ns.expand_parent != {} {
		ch, nat_ns, chok := _inner_chain_to_native(s, ns)
		if !chok || nat_ns == nil {
			// Couldn't resolve chain — drop diff entries to avoid leaking.
			for &ov in diff {
				delete(ov.property_path)
				json.destroy_value(ov.value)
			}
			return
		}
		native_ns = nat_ns
		chain = ch
	}

	for &ov in diff {
		target_lid := ov.target
		if ns.expand_parent != {} {
			// Deep override: materialize a breadcrumb on the native NS keyed
			// by the chain + final destination in ns.source_prefab namespace.
			final_src := PPtr{guid = ns.source_prefab, local_id = ov.target}
			peg, pok := breadcrumb_create(s, native_ns.local_id, final_src, chain[:])
			if !pok {
				delete(ov.property_path)
				json.destroy_value(ov.value)
				continue
			}
			target_lid = peg
		}

		// Append on native NS, deduping (target, property_path).
		dup := false
		for &existing in native_ns.overrides {
			if existing.target == target_lid && existing.property_path == ov.property_path {
				dup = true
				break
			}
		}
		if dup {
			delete(ov.property_path)
			json.destroy_value(ov.value)
			continue
		}
		append(&native_ns.overrides, Override{
			target        = target_lid,
			property_path = ov.property_path,
			value         = ov.value,
		})
	}
}

// Returns the set of local_ids that appear in the prefab base file's section
// arrays. Used by the override cleanup pass.
_collect_prefab_local_ids :: proc(base_raw: []byte, allocator := context.allocator) -> (map[Local_ID]bool, bool) {
	out := make(map[Local_ID]bool, 0, allocator)
	base_copy := make([]byte, len(base_raw), context.temp_allocator)
	copy(base_copy, base_raw)
	val: json.Value
	if json.unmarshal_string(string(base_copy), &val) != nil {
		delete(out)
		return nil, false
	}
	defer json.destroy_value(val)
	root, is_obj := val.(json.Object)
	if !is_obj {
		delete(out)
		return nil, false
	}
	for _, section_val in root {
		arr, is_arr := section_val.(json.Array)
		if !is_arr do continue
		for item in arr {
			obj, ok := item.(json.Object)
			if !ok do continue
			lid, lid_ok := _scene_file_local_id_of(obj)
			if lid_ok do out[lid] = true
		}
	}
	return out, true
}

_scene_file_local_id_of :: proc(obj: json.Object) -> (Local_ID, bool) {
	from_value :: proc(v: json.Value) -> (Local_ID, bool) {
		if f, ok := v.(json.Float);   ok do return Local_ID(f), true
		if i, ok := v.(json.Integer); ok do return Local_ID(i), true
		return 0, false
	}
	if v, ok := obj["local_id"]; ok do return from_value(v)
	if bv, ok := obj["base"]; ok {
		if bo, ok2 := bv.(json.Object); ok2 {
			if v, ok3 := bo["local_id"]; ok3 do return from_value(v)
		}
	}
	return 0, false
}

_drop_overrides_with_missing_targets :: proc(overrides: ^[dynamic]Override, base_raw: []byte) {
	ids, ok := _collect_prefab_local_ids(base_raw)
	if !ok do return
	defer delete(ids)
	write := 0
	for i in 0 ..< len(overrides) {
		ov := overrides[i]
		if ids[ov.target] {
			overrides[write] = ov
			write += 1
		} else {
			delete(ov.property_path)
			json.destroy_value(ov.value)
		}
	}
	resize(overrides, write)
}

_find_ns_by_local_id :: proc(s: ^Scene, ns_local_id: Local_ID) -> (^NestedScene, bool) {
	for &ns in s.nested_scenes {
		if ns.local_id == ns_local_id do return &ns, true
	}
	return nil, false
}

// Walks `inner_m`'s ancestry (via expand_parent) and returns the chain of
// prefab hops from the native NS down to (but not including) `inner_m`'s own
// hop, plus the native NS itself. The chain is top-down: chain[0] is the
// hop from the native NS into the next inner level. Each entry is a PPtr
// (prefab guid, host transform local_id in PARENT prefab namespace).
@(private = "file")
_inner_chain_to_native :: proc(s: ^Scene, inner_m: ^NestedScene) -> ([dynamic]PPtr, ^NestedScene, bool) {
	chain := make([dynamic]PPtr, 0, 4, context.temp_allocator)
	if inner_m == nil || inner_m.expand_parent == {} do return chain, nil, false
	w := ctx_world()

	// Walk: append `inner_m`'s OWN hop first, then walk up adding each
	// outer inner_m's hop. Stop at the native level.
	append(&chain, PPtr{local_id = inner_m.transform_parent, guid = inner_m.source_prefab})

	cur := inner_m
	for _ in 0 ..< 32 {
		ep := cur.expand_parent
		if ep == {} do return chain, nil, false
		et := pool_get(&w.transforms, Handle(ep))
		if et == nil do return chain, nil, false
		if !et.nested_owned {
			// ep belongs to the native scene — find its native NS.
			for &n2 in s.nested_scenes {
				if n2.expand_parent != {} do continue
				if !nested_scene_hosts_transform(s, &n2, ep) do continue
				// Reverse so chain becomes top-down.
				n := len(chain)
				for i in 0 ..< n / 2 {
					chain[i], chain[n - 1 - i] = chain[n - 1 - i], chain[i]
				}
				return chain, &n2, true
			}
			return chain, nil, false
		}
		// ep is a nested-owned host transform — owner must be another inner NS.
		next: ^NestedScene = nil
		for &n2 in s.nested_scenes {
			if n2.expand_parent == {} do continue
			if nested_scene_hosts_transform(s, &n2, ep) {
				next = &n2
				break
			}
		}
		if next == nil do return chain, nil, false
		append(&chain, PPtr{local_id = next.transform_parent, guid = next.source_prefab})
		cur = next
	}
	return chain, nil, false
}

scene_save :: proc(s: ^Scene, path: string) -> bool {
	if s == nil do return false
	w := ctx_world()

	// Per docs/NestedPrefabs.md, overrides live at the root scene level only.
	// Capture writes directly onto each chain's native NS; inner-NS records
	// keep the overrides they loaded from their inner-prefab files (those are
	// runtime-only — used by per-level shallow bake during resolve, never
	// persisted by save's filter). Clear native NS overrides so the diff
	// repopulates from scratch.
	for &ns in s.nested_scenes {
		if ns.expand_parent != {} do continue
		for &ov in ns.overrides {
			delete(ov.property_path)
			json.destroy_value(ov.value)
		}
		clear(&ns.overrides)
	}
	for &ns in s.nested_scenes {
		_capture_overrides_to_native(s, &ns)
	}

	// Prune orphan breadcrumbs whose owning NS no longer references them as a
	// host peg or override target. Cross-scene Handle pegs (no NS owner) are
	// left alone — they're referenced via Handle/PPtr fields elsewhere, not
	// through NS overrides. Without this, repeatedly editing-reverting a deep
	// field accumulates dead breadcrumb entries that bloat the file.
	{
		ns_referenced := make(map[Local_ID]bool, 0, context.temp_allocator)
		for &ns in s.nested_scenes {
			if ns.host_breadcrumb_id != 0 do ns_referenced[ns.host_breadcrumb_id] = true
			for &ov in ns.overrides do ns_referenced[ov.target] = true
		}
		to_drop := make([dynamic]Local_ID, 0, 8, context.temp_allocator)
		for lid, bc in s.breadcrumb_data {
			if _, has_owner := _find_ns_by_local_id(s, bc.scene_instance); !has_owner do continue
			if !ns_referenced[lid] do append(&to_drop, lid)
		}
		for lid in to_drop do breadcrumb_remove(s, lid)
	}

	sf := SceneFile{}
	sf.next_local_id = s.next_local_id

	// Only persist NS records that belong to this scene file. Records with
	// `expand_parent` set were pulled in from inner prefabs during resolve
	// (see nested_scene_resolve at nested_scene.odin:582) and live in
	// `s.nested_scenes` purely for in-memory operations — saving them would
	// duplicate the inner prefab's metadata into this file and, on reload,
	// turn the inner host transforms into ghost nested-scene hosts.
	native_ns_lids := make(map[Local_ID]bool, 0, context.temp_allocator)
	for &ns in s.nested_scenes {
		if ns.expand_parent != {} do continue
		append(&sf.nested_scenes, ns)
		native_ns_lids[ns.local_id] = true
	}
	for _, bc in s.breadcrumb_data {
		// Keep only breadcrumbs whose owning NestedScene is also native (or
		// that aren't host pegs at all — cross-scene Handle pegs survive).
		if native_ns_lids[bc.scene_instance] {
			append(&sf.breadcrumbs, bc)
		} else if _, has_owner := _find_ns_by_local_id(s, bc.scene_instance); !has_owner {
			append(&sf.breadcrumbs, bc)
		}
	}

	if s.root.handle != {} {
		t := pool_get(&w.transforms, s.root.handle)
		if t != nil {
			sf.root = t.local_id
			_collect_transform_tree(w, Transform_Handle(s.root.handle), &sf)
		}
	}

	// Repair next_local_id: any local_id present in the file must be strictly
	// less than next_local_id. Otherwise a future scene_next_id() collides with
	// an existing entity, which on reload can cause a regular transform to be
	// matched as the host of a NestedScene record.
	bump :: proc(m: ^Local_ID, v: Local_ID) { if v >= m^ do m^ = v + 1 }
	for &tr in sf.transforms {
		bump(&sf.next_local_id, tr.local_id)
		for &c in tr.components do bump(&sf.next_local_id, c.local_id)
	}
	for &c in sf.cameras          do bump(&sf.next_local_id, c.local_id)
	for &c in sf.lifetimes        do bump(&sf.next_local_id, c.local_id)
	for &c in sf.players          do bump(&sf.next_local_id, c.local_id)
	for &c in sf.scripts          do bump(&sf.next_local_id, c.local_id)
	for &c in sf.sprite_renderers do bump(&sf.next_local_id, c.local_id)
	for &ns in sf.nested_scenes   do bump(&sf.next_local_id, ns.local_id)
	for &bc in sf.breadcrumbs     do bump(&sf.next_local_id, bc.local_id)
	s.next_local_id = sf.next_local_id

	opts := json.Marshal_Options{
		spec       = .JSON,
		pretty     = true,
		use_spaces = true,
		spaces     = 2,
	}
	data, err := json.marshal(sf, opts)
	if err != nil {
		fmt.printf("[Scene] Failed to marshal scene: %v\n", err)
		scene_file_destroy_shallow(&sf)
		return false
	}
	defer delete(data)

	scene_file_destroy_shallow(&sf)

	if write_err := os.write_entire_file(path, data); write_err != nil {
		fmt.printf("[Scene] Failed to write file: %s — %v\n", path, write_err)
		return false
	}

	if s.path != path {
		delete(s.path)
		s.path = strings.clone(path)
	}

	// Per docs/NestedPrefabs.md "Changes propagation": saving a prefab walks
	// all live `NestedScene` records whose `source_prefab` GUID matches the
	// saved asset and reloads them. Refresh `scene_lib`'s cached bytes for
	// this asset, drop the unpacked-snapshot cache, and re-resolve every
	// native NS whose chain transitively contains this guid in any loaded
	// scene — including the scene we just saved (its own nested instances of
	// itself, if any, plus any sibling NSs that depend on it via inner chain).
	if guid, gok := asset_db_get_guid(path); gok {
		asset_guid := Asset_GUID(guid)
		if existing, has := scene_lib[asset_guid]; has do delete(existing)
		fresh := make([]byte, len(data))
		copy(fresh, data)
		scene_lib[asset_guid] = fresh
		scene_lib_unpacked_invalidate(asset_guid)
		_propagate_prefab_save(asset_guid)
	}

	fmt.printf("[Scene] Saved scene to %s\n", path)
	return true
}

// Walks all loaded scenes and re-resolves every native NS whose chain
// transitively contains `saved_guid`. "Contains" means the native NS itself
// has source_prefab == saved_guid, OR any inner NS under it (any NS with
// expand_parent in that native's resolved subtree) has source_prefab ==
// saved_guid. Re-resolving the native rebuilds its entire subtree, picking
// up the freshly-saved prefab content.
@(private = "file")
_propagate_prefab_save :: proc(saved_guid: Asset_GUID) {
	sm := ctx_scene_manager()
	for i in 0 ..< sm.count {
		s := sm.loaded[i]
		if s == nil do continue

		// Collect native NS local_ids whose chain involves saved_guid.
		to_reresolve := make([dynamic]Local_ID, 0, 8, context.temp_allocator)
		for &ns in s.nested_scenes {
			if ns.source_prefab != saved_guid do continue
			// Walk to the native ancestor.
			cur := &ns
			if cur.expand_parent == {} {
				append(&to_reresolve, cur.local_id)
				continue
			}
			for _ in 0 ..< 64 {
				ep := cur.expand_parent
				if ep == {} {
					append(&to_reresolve, cur.local_id)
					break
				}
				outer := scene_find_nested_scene_for_host(s, ep)
				if outer == nil do break
				if outer.expand_parent == {} {
					append(&to_reresolve, outer.local_id)
					break
				}
				cur = outer
			}
		}

		// Dedup.
		seen := make(map[Local_ID]bool, 0, context.temp_allocator)
		for lid in to_reresolve {
			if seen[lid] do continue
			seen[lid] = true
			ns_ptr, has := scene_nested_scene_by_local_id(s, lid)
			if !has || ns_ptr == nil do continue
			host_tH := nested_scene_resolve_host_handle(s, ns_ptr)
			if host_tH == {} do continue
			nested_scene_resolve(host_tH)
		}
	}
}

scene_file_load :: proc(filepath: string) -> (SceneFile, bool) {
	data, read_ok := os.read_entire_file(filepath, context.allocator)
	if read_ok != nil {
		fmt.printf("[Scene] Failed to read file: %s\n", filepath)
		return {}, false
	}
	defer delete(data)

	sf: SceneFile
	unmarshal_err := json.unmarshal(data, &sf)
	if unmarshal_err != nil {
		fmt.printf("[Scene] Failed to unmarshal scene: %v\n", unmarshal_err)
		return {}, false
	}

	return sf, true
}

resolve_handle :: proc(local_id: Local_ID, id_map: map[Local_ID]Handle) -> (Handle, bool) {
	if local_id == 0 do return {}, false
	if h, ok := id_map[local_id]; ok {
		return h, true
	}
	return {}, false
}

scene_load_single_path :: proc(path: string) -> ^Scene {
	sf, ok := scene_file_load(path)
	if !ok do return nil
	defer scene_file_destroy(&sf)

	scene_guid: Asset_GUID = {}
	if g, gok := asset_db_get_guid(path); gok {
		scene_guid = Asset_GUID(g)
	}
	s := _scene_load_single(&sf, scene_guid)
	if s != nil {
		s.path = strings.clone(path)
	}
	return s
}

scene_load_additive_path :: proc(path: string) -> ^Scene {
	sf, ok := scene_file_load(path)
	if !ok do return nil
	defer scene_file_destroy(&sf)

	scene_guid: Asset_GUID = {}
	if g, gok := asset_db_get_guid(path); gok {
		scene_guid = Asset_GUID(g)
	}
	s := _scene_load_additive(&sf, scene_guid)
	if s != nil {
		s.path = strings.clone(path)
	}
	return s
}

scene_copy_subtree :: proc(tH: Transform_Handle) -> []byte {
	w := ctx_world()
	t := pool_get(&w.transforms, Handle(tH))
	if t == nil do return nil

	sf := SceneFile{}
	sf.root = t.local_id
	_collect_transform_tree(w, tH, &sf)
	defer scene_file_destroy_shallow(&sf)

	opts := json.Marshal_Options{spec = .JSON, pretty = false}
	data, err := json.marshal(sf, opts)
	if err != nil {
		fmt.printf("[Scene] Failed to marshal subtree: %v\n", err)
		delete(data)
		return nil
	}
	return data
}

scene_paste_subtree :: proc(data: []byte, parent: Transform_Handle) -> Transform_Handle {
	if parent == {} || len(data) == 0 do return {}
	w := ctx_world()
	if !pool_valid(&w.transforms, Handle(parent)) do return {}

	sf: SceneFile
	if err := json.unmarshal(data, &sf); err != nil {
		fmt.printf("[Scene] Failed to unmarshal subtree: %v\n", err)
		return {}
	}
	defer scene_file_destroy(&sf)

	pt := pool_get(&w.transforms, Handle(parent))
	s := pt.scene

	_scene_file_remap_local_ids(&sf, s)
	root_tH := _scene_load_as_child(&sf, parent, s)
	if root_tH != {} && !ctx_get().is_playmode {
		_scene_resolve_nested_in_subtree(root_tH)
	}
	return root_tH
}

scene_duplicate_subtree :: proc(tH: Transform_Handle) -> Transform_Handle {
	w := ctx_world()
	t := pool_get(&w.transforms, Handle(tH))
	if t == nil do return {}

	parent := Transform_Handle(t.parent.handle)
	if !pool_valid(&w.transforms, Handle(parent)) do return {}

	data := scene_copy_subtree(tH)
	defer delete(data)

	return scene_paste_subtree(data, parent)
}
